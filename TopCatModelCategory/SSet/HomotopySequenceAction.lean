import TopCatModelCategory.SSet.HomotopySequence
import TopCatModelCategory.SSet.FundamentalGroupoidPiOne

universe u

open CategoryTheory SSet.modelCategoryQuillen HomotopicalAlgebra
  Simplicial Opposite

namespace SSet

namespace KanComplex.FundamentalGroupoid

variable {E B : SSet.{u}} (p : E ‚ü∂ B)
  {b‚ÇÄ b‚ÇÅ : B _‚¶ã0‚¶å} (f : Edge (.mk b‚ÇÄ) (.mk b‚ÇÅ))

structure FiberActionStruct (e‚ÇÄ e‚ÇÅ : E _‚¶ã0‚¶å) where
  map : Œî[1] ‚ü∂ E
  map_p : map ‚â´ p = f.map := by aesop_cat
  Œ¥‚ÇÅ_map : stdSimplex.Œ¥ 1 ‚â´ map = const e‚ÇÄ := by aesop_cat
  Œ¥‚ÇÄ_map : stdSimplex.Œ¥ 0 ‚â´ map = const e‚ÇÅ := by aesop_cat

namespace FiberActionStruct

attribute [reassoc (attr := simp)] map_p Œ¥‚ÇÅ_map Œ¥‚ÇÄ_map

section

variable {p f} {e‚ÇÄ e‚ÇÅ : E _‚¶ã0‚¶å} (h : FiberActionStruct p f e‚ÇÄ e‚ÇÅ)

include h in
lemma app_zero : p.app _ e‚ÇÄ = b‚ÇÄ := by
  have := h.Œ¥‚ÇÅ_map =‚â´ p
  rw [Category.assoc, map_p, const_comp] at this
  apply yonedaEquiv.symm.injective
  simp only [yonedaEquiv_symm_zero, ‚Üê this, f.comm‚ÇÄ]

include h in
lemma app_one : p.app _ e‚ÇÅ = b‚ÇÅ := by
  have := h.Œ¥‚ÇÄ_map =‚â´ p
  rw [Category.assoc, map_p, const_comp] at this
  apply yonedaEquiv.symm.injective
  simp only [yonedaEquiv_symm_zero, ‚Üê this, f.comm‚ÇÅ]

end

@[simps]
protected def const {e‚ÇÄ : E _‚¶ã0‚¶å} (h‚ÇÄ : p.app _ e‚ÇÄ = b‚ÇÄ) :
    FiberActionStruct p (.id (.mk b‚ÇÄ)) e‚ÇÄ e‚ÇÄ where
  map := SSet.const e‚ÇÄ
  map_p := by simp [h‚ÇÄ]
  Œ¥‚ÇÄ_map := by simp
  Œ¥‚ÇÅ_map := by simp

def ofEdge {b‚ÇÄ : B _‚¶ã0‚¶å} {e‚ÇÄ e‚ÇÅ : (Subcomplex.fiber p b‚ÇÄ : SSet) _‚¶ã0‚¶å}
    (e : Edge (.mk e‚ÇÄ) (.mk e‚ÇÅ)) :
    FiberActionStruct p (.id (.mk b‚ÇÄ)) e‚ÇÄ e‚ÇÅ := by
  obtain ‚ü®e‚ÇÄ, he‚ÇÄ‚ü© := e‚ÇÄ
  obtain ‚ü®e‚ÇÅ, he‚ÇÅ‚ü© := e‚ÇÅ
  simp only [Subcomplex.mem_fiber_obj_zero_iff] at he‚ÇÄ he‚ÇÅ
  refine {
    map := e.map ‚â´ Subcomplex.Œπ _
    map_p := by
      simp
    Œ¥‚ÇÄ_map := by simp [e.comm‚ÇÅ_assoc]
    Œ¥‚ÇÅ_map := by simp [e.comm‚ÇÄ_assoc]
  }

noncomputable def ofœÄ‚ÇÄRel {b‚ÇÄ : B _‚¶ã0‚¶å} {e‚ÇÄ e‚ÇÅ : (Subcomplex.fiber p b‚ÇÄ : SSet) _‚¶ã0‚¶å}
    (h : œÄ‚ÇÄRel e‚ÇÄ e‚ÇÅ) :
    FiberActionStruct p (.id (.mk b‚ÇÄ)) e‚ÇÄ e‚ÇÅ := Nonempty.some (by
  obtain ‚ü®c, hc‚ÇÄ, hc‚ÇÅ‚ü© := h
  refine ‚ü®ofEdge _ (Edge.mk (yonedaEquiv.symm c) ?_ ?_)‚ü©
  ¬∑ rw [stdSimplex.Œ¥_comp_yonedaEquiv_symm, hc‚ÇÄ, yonedaEquiv_symm_eq_const]
  ¬∑ rw [stdSimplex.Œ¥_comp_yonedaEquiv_symm, hc‚ÇÅ, yonedaEquiv_symm_eq_const])

section

variable {p} {e‚ÇÄ e‚ÇÅ : E _‚¶ã0‚¶å} (h : FiberActionStruct p (.id (.mk b‚ÇÄ)) e‚ÇÄ e‚ÇÅ)

def toFibreOneSimplex : (Subcomplex.fiber p b‚ÇÄ : SSet) _‚¶ã1‚¶å :=
  yonedaEquiv (Subcomplex.lift h.map (by simp))

@[simp]
lemma Œ¥_one_toFibreOneSimplex :
    (Subcomplex.fiber p b‚ÇÄ : SSet).Œ¥ 1 h.toFibreOneSimplex = ‚ü®e‚ÇÄ, by simp [h.app_zero]‚ü© := by
  dsimp
  ext
  change E.map (SimplexCategory.Œ¥ 1).op (yonedaEquiv h.map) = e‚ÇÄ
  apply yonedaEquiv.symm.injective
  rw [yonedaEquiv_symm_map, Equiv.symm_apply_apply, yonedaEquiv_symm_zero]
  exact h.Œ¥‚ÇÅ_map

@[simp]
lemma Œ¥_zero_toFibreOneSimplex :
    (Subcomplex.fiber p b‚ÇÄ : SSet).Œ¥ 0 h.toFibreOneSimplex = ‚ü®e‚ÇÅ, by simp [h.app_one]‚ü© := by
  dsimp
  ext
  change E.map (SimplexCategory.Œ¥ 0).op (yonedaEquiv h.map) = e‚ÇÅ
  apply yonedaEquiv.symm.injective
  rw [yonedaEquiv_symm_map, Equiv.symm_apply_apply, yonedaEquiv_symm_zero]
  exact h.Œ¥‚ÇÄ_map

end

variable [Fibration p]

lemma nonempty (e‚ÇÄ : E _‚¶ã0‚¶å) (h‚ÇÄ : p.app _ e‚ÇÄ = b‚ÇÄ) :
    ‚àÉ (e‚ÇÅ : E _‚¶ã0‚¶å), Nonempty (FiberActionStruct p f e‚ÇÄ e‚ÇÅ) := by
  have := (anodyneExtensions.Œ¥_mem 1).hasLeftLiftingProperty p
  have sq : CommSq (const e‚ÇÄ) (stdSimplex.Œ¥ 1) p f.map := ‚ü®by simp [f.comm‚ÇÄ, h‚ÇÄ]‚ü©
  exact ‚ü®yonedaEquiv (stdSimplex.Œ¥ 0 ‚â´ sq.lift), ‚ü®{
    map := sq.lift
    Œ¥‚ÇÄ_map := yonedaEquiv.injective (by simp)
  }‚ü©‚ü©

lemma nonempty' (e‚ÇÅ : E _‚¶ã0‚¶å) (h‚ÇÅ : p.app _ e‚ÇÅ = b‚ÇÅ) :
    ‚àÉ (e‚ÇÄ : E _‚¶ã0‚¶å), Nonempty (FiberActionStruct p f e‚ÇÄ e‚ÇÅ) := by
  have := (anodyneExtensions.Œ¥_mem 0).hasLeftLiftingProperty p
  have sq : CommSq (const e‚ÇÅ) (stdSimplex.Œ¥ 0) p f.map := ‚ü®by simp [f.comm‚ÇÅ, h‚ÇÅ]‚ü©
  exact ‚ü®yonedaEquiv (stdSimplex.Œ¥ 1 ‚â´ sq.lift), ‚ü®{
    map := sq.lift
    Œ¥‚ÇÅ_map := yonedaEquiv.injective (by simp)
  }‚ü©‚ü©

variable {p f}

noncomputable def comp {b‚ÇÇ : B _‚¶ã0‚¶å} {f‚ÇÄ‚ÇÅ : Edge (.mk b‚ÇÄ) (.mk b‚ÇÅ)}
    {e‚ÇÄ e‚ÇÅ e‚ÇÇ : E _‚¶ã0‚¶å} (h‚ÇÄ‚ÇÅ : FiberActionStruct p f‚ÇÄ‚ÇÅ e‚ÇÄ e‚ÇÅ)
    {f‚ÇÅ‚ÇÇ : Edge (.mk b‚ÇÅ) (.mk b‚ÇÇ)}
    (h‚ÇÅ‚ÇÇ : FiberActionStruct p f‚ÇÅ‚ÇÇ e‚ÇÅ e‚ÇÇ)
    {f‚ÇÄ‚ÇÇ : Edge (.mk b‚ÇÄ) (.mk b‚ÇÇ)}
    (h : Edge.CompStruct f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ) :
    FiberActionStruct p f‚ÇÄ‚ÇÇ e‚ÇÄ e‚ÇÇ := Nonempty.some (by
  obtain ‚ü®œÜ, hœÜ‚ÇÅ, hœÜ‚ÇÇ‚ü© := horn‚ÇÇ‚ÇÅ.isPushout.exists_desc h‚ÇÄ‚ÇÅ.map h‚ÇÅ‚ÇÇ.map (by simp)
  have sq : CommSq œÜ Œõ[2,1].Œπ p h.map := ‚ü®by
    apply horn‚ÇÇ‚ÇÅ.isPushout.hom_ext
    ¬∑ simp [reassoc_of% hœÜ‚ÇÅ]
    ¬∑ simp [reassoc_of% hœÜ‚ÇÇ]‚ü©
  exact ‚ü®{
    map := stdSimplex.Œ¥ 1 ‚â´ sq.lift
    Œ¥‚ÇÄ_map := by
      have h‚ÇÅ := stdSimplex.{u}.Œ¥_comp_Œ¥_self (n := 0) (i := 0)
      dsimp at h‚ÇÅ
      have h‚ÇÇ := horn‚ÇÇ‚ÇÅ.Œπ‚ÇÅ‚ÇÇ ‚â´= sq.fac_left
      rw [horn.Œπ_Œπ_assoc, hœÜ‚ÇÇ] at h‚ÇÇ
      rw [‚Üê reassoc_of% h‚ÇÅ, h‚ÇÇ, Œ¥‚ÇÄ_map]
    Œ¥‚ÇÅ_map := by
      have h‚ÇÅ := stdSimplex.{u}.Œ¥_comp_Œ¥_self (n := 0) (i := 1)
      dsimp at h‚ÇÅ
      have h‚ÇÇ := horn‚ÇÇ‚ÇÅ.Œπ‚ÇÄ‚ÇÅ ‚â´= sq.fac_left
      rw [horn.Œπ_Œπ_assoc, hœÜ‚ÇÅ] at h‚ÇÇ
      rw [reassoc_of% h‚ÇÅ, h‚ÇÇ, Œ¥‚ÇÅ_map]
  }‚ü©)

noncomputable def symm {e‚ÇÄ e‚ÇÅ : E _‚¶ã0‚¶å} (h : FiberActionStruct p f e‚ÇÄ e‚ÇÅ)
    {f' : Edge (.mk b‚ÇÅ) (.mk b‚ÇÄ)} (hff' : Edge.CompStruct f' f (.id (.mk b‚ÇÅ))) :
    FiberActionStruct p f' e‚ÇÅ e‚ÇÄ := Nonempty.some (by
  obtain ‚ü®œÜ, hœÜ‚ÇÅ, hœÜ‚ÇÇ‚ü© := horn‚ÇÇ‚ÇÇ.isPushout.exists_desc (const e‚ÇÅ) h.map (by simp)
  have sq : CommSq œÜ Œõ[2,2].Œπ p hff'.map := ‚ü®by
    apply horn‚ÇÇ‚ÇÇ.isPushout.hom_ext
    ¬∑ simp [reassoc_of% hœÜ‚ÇÅ, h.app_one]
    ¬∑ simp [reassoc_of% hœÜ‚ÇÇ]‚ü©
  exact ‚ü®{
    map := stdSimplex.Œ¥ 2 ‚â´ sq.lift
    Œ¥‚ÇÄ_map := by
      have h‚ÇÅ := stdSimplex.{u}.Œ¥_comp_Œ¥ (n := 0) (i := 0) (j := 1) (by simp)
      dsimp at h‚ÇÅ
      have h‚ÇÇ := horn‚ÇÇ‚ÇÇ.Œπ‚ÇÅ‚ÇÇ ‚â´= sq.fac_left
      rw [horn.Œπ_Œπ_assoc] at h‚ÇÇ
      rw [reassoc_of% h‚ÇÅ, h‚ÇÇ, hœÜ‚ÇÇ, Œ¥‚ÇÅ_map]
    Œ¥‚ÇÅ_map := by
      have h‚ÇÅ := stdSimplex.{u}.Œ¥_comp_Œ¥ (n := 0) (i := 1) (j := 1) (by simp)
      dsimp at h‚ÇÅ
      have h‚ÇÇ := horn‚ÇÇ‚ÇÇ.Œπ‚ÇÄ‚ÇÇ ‚â´= sq.fac_left
      rw [horn.Œπ_Œπ_assoc] at h‚ÇÇ
      rw [reassoc_of% h‚ÇÅ, h‚ÇÇ, hœÜ‚ÇÅ, comp_const]
  }‚ü©)

noncomputable def unique
    [IsFibrant B] {e‚ÇÄ e‚ÇÄ' e‚ÇÅ e‚ÇÅ' : E _‚¶ã0‚¶å} (h : FiberActionStruct p f e‚ÇÄ e‚ÇÅ)
    {f' : Edge (.mk b‚ÇÄ) (.mk b‚ÇÅ)} (h' : FiberActionStruct p f' e‚ÇÄ' e‚ÇÅ')
    (hff' : f.Homotopy f')
    (he : FiberActionStruct p (.id (.mk b‚ÇÄ)) e‚ÇÄ e‚ÇÄ') :
    FiberActionStruct p (.id (.mk b‚ÇÅ)) e‚ÇÅ e‚ÇÅ' :=
  Nonempty.some (by
    obtain ‚ü®g, ‚ü®hg‚ü©‚ü© := Edge.CompStruct.left_inverse f
    exact ‚ü®comp (comp (h.symm hg) he (Edge.CompStruct.compId g))
      h' (hg.assoc' hff'.homotopyL (.idComp _))‚ü©)

end FiberActionStruct

section

variable [Fibration p]

noncomputable def fiberAction (e‚ÇÄ : E _‚¶ã0‚¶å) (h‚ÇÄ : p.app _ e‚ÇÄ = b‚ÇÄ) : E _‚¶ã0‚¶å :=
  (FiberActionStruct.nonempty p f e‚ÇÄ h‚ÇÄ).choose

noncomputable def fiberActionStruct (e‚ÇÄ : E _‚¶ã0‚¶å) (h‚ÇÄ : p.app _ e‚ÇÄ = b‚ÇÄ) :
    FiberActionStruct p f e‚ÇÄ (fiberAction p f e‚ÇÄ h‚ÇÄ) :=
  (FiberActionStruct.nonempty p f e‚ÇÄ h‚ÇÄ).choose_spec.some

variable [IsFibrant B]

noncomputable def œÄ‚ÇÄFiberAction {b‚ÇÄ b‚ÇÅ : FundamentalGroupoid B} :
    (b‚ÇÄ ‚ü∂ b‚ÇÅ) ‚Üí œÄ‚ÇÄ (Subcomplex.fiber p b‚ÇÄ.pt) ‚Üí œÄ‚ÇÄ (Subcomplex.fiber p b‚ÇÅ.pt) :=
  Quot.lift‚ÇÇ
    (fun q e‚ÇÄ ‚Ü¶ œÄ‚ÇÄ.mk ‚ü®fiberAction p q e‚ÇÄ
      (by simpa only [Subcomplex.mem_fiber_obj_zero_iff] using e‚ÇÄ.2), by
        simpa only [Subcomplex.mem_fiber_obj_zero_iff] using
          (fiberActionStruct p q e‚ÇÄ _).app_one‚ü©)
    (fun q e‚ÇÄ e‚ÇÄ' c ‚Ü¶
      œÄ‚ÇÄ.sound (FiberActionStruct.unique
        (fiberActionStruct p q e‚ÇÄ
          (by simpa only [Subcomplex.mem_fiber_obj_zero_iff] using e‚ÇÄ.2))
        (fiberActionStruct p q e‚ÇÄ'
          (by simpa only [Subcomplex.mem_fiber_obj_zero_iff] using e‚ÇÄ'.2))
        (.refl q) (.ofœÄ‚ÇÄRel p c)).toFibreOneSimplex (by simp) (by simp))
    (fun q q' e‚ÇÄ ‚ü®h‚ü© ‚Ü¶
      œÄ‚ÇÄ.sound (FiberActionStruct.unique
        (fiberActionStruct p q e‚ÇÄ
          (by simpa only [Subcomplex.mem_fiber_obj_zero_iff] using e‚ÇÄ.2))
        (fiberActionStruct p q' e‚ÇÄ
          (by simpa only [Subcomplex.mem_fiber_obj_zero_iff] using e‚ÇÄ.2))
        h (.const p
          (by simpa only [Subcomplex.mem_fiber_obj_zero_iff] using e‚ÇÄ.2))).toFibreOneSimplex
        (by simp) (by simp))

variable {p} in
lemma FiberActionStruct.œÄ‚ÇÄFiberAction_eq
    {b‚ÇÄ b‚ÇÅ : FundamentalGroupoid B} {f : b‚ÇÄ.Edge b‚ÇÅ}
    {e‚ÇÄ e‚ÇÅ : E _‚¶ã0‚¶å}
    (h : FiberActionStruct p f e‚ÇÄ e‚ÇÅ) :
    œÄ‚ÇÄFiberAction p (FundamentalGroupoid.homMk f) (œÄ‚ÇÄ.mk ‚ü®e‚ÇÄ, by
      simp only [Subcomplex.mem_fiber_obj_zero_iff, h.app_zero]‚ü©) =
    œÄ‚ÇÄ.mk (X := Subcomplex.fiber p b‚ÇÅ.pt) ‚ü®e‚ÇÅ, by
      simp only [Subcomplex.mem_fiber_obj_zero_iff, h.app_one]‚ü© :=
  œÄ‚ÇÄ.sound
    (FiberActionStruct.unique (fiberActionStruct p f e‚ÇÄ h.app_zero) h (.refl _)
        (.const _ h.app_zero)).toFibreOneSimplex
      (by simp) (by simp)

lemma œÄ‚ÇÄFiberAction_mk_eq_iff
    {b‚ÇÄ b‚ÇÅ : FundamentalGroupoid B} (f : b‚ÇÄ.Edge b‚ÇÅ)
    (e‚ÇÄ : (Subcomplex.fiber p b‚ÇÄ.pt : SSet) _‚¶ã0‚¶å)
    (e‚ÇÅ : (Subcomplex.fiber p b‚ÇÅ.pt : SSet) _‚¶ã0‚¶å) :
    œÄ‚ÇÄFiberAction p (FundamentalGroupoid.homMk f) (œÄ‚ÇÄ.mk e‚ÇÄ) = œÄ‚ÇÄ.mk e‚ÇÅ ‚Üî
      Nonempty (FiberActionStruct p f e‚ÇÄ e‚ÇÅ) := by
  constructor
  ¬∑ intro h
    change œÄ‚ÇÄ.mk _ = _ at h
    rw [KanComplex.œÄ‚ÇÄ_mk_eq_œÄ‚ÇÄ_mk_iff] at h
    obtain ‚ü®h‚ü© := h
    exact ‚ü®FiberActionStruct.comp (fiberActionStruct p f e‚ÇÄ
      (by simpa only [Subcomplex.mem_fiber_obj_zero_iff] using e‚ÇÄ.2))
      (FiberActionStruct.ofEdge p h) (.compId _)‚ü©
  ¬∑ rintro ‚ü®h‚ü©
    exact h.œÄ‚ÇÄFiberAction_eq

lemma œÄ‚ÇÄFiberAction_id (b‚ÇÄ : FundamentalGroupoid B)
    (s : œÄ‚ÇÄ (Subcomplex.fiber p b‚ÇÄ.pt)) :
    œÄ‚ÇÄFiberAction p (ùüô b‚ÇÄ) s = s := by
  obtain ‚ü®e‚ÇÄ, rfl‚ü© := s.mk_surjective
  apply FiberActionStruct.œÄ‚ÇÄFiberAction_eq
  exact .const _ (by simpa only [Subcomplex.mem_fiber_obj_zero_iff] using e‚ÇÄ.2)

lemma œÄ‚ÇÄFiberAction_comp {b‚ÇÄ b‚ÇÅ b‚ÇÇ : FundamentalGroupoid B}
    (f : b‚ÇÄ ‚ü∂ b‚ÇÅ) (g : b‚ÇÅ ‚ü∂ b‚ÇÇ) (s : œÄ‚ÇÄ (Subcomplex.fiber p b‚ÇÄ.pt)) :
    œÄ‚ÇÄFiberAction p (f ‚â´ g) s = œÄ‚ÇÄFiberAction p g (œÄ‚ÇÄFiberAction p f s) := by
  obtain ‚ü®‚ü®e‚ÇÄ, he‚ÇÄ‚ü©, rfl‚ü© := s.mk_surjective
  obtain ‚ü®f‚ÇÄ‚ÇÅ, rfl‚ü© := homMk_surjective f
  obtain ‚ü®f‚ÇÅ‚ÇÇ, rfl‚ü© := homMk_surjective g
  obtain ‚ü®f‚ÇÄ‚ÇÇ, ‚ü®h‚ü©‚ü© := Edge.exists_compStruct f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ
  obtain ‚ü®e‚ÇÅ, ‚ü®h‚ÇÄ‚ÇÅ‚ü©‚ü© := FiberActionStruct.nonempty p f‚ÇÄ‚ÇÅ e‚ÇÄ (by simpa using he‚ÇÄ)
  obtain ‚ü®e‚ÇÇ, ‚ü®h‚ÇÅ‚ÇÇ‚ü©‚ü© := FiberActionStruct.nonempty p f‚ÇÅ‚ÇÇ e‚ÇÅ (h‚ÇÄ‚ÇÅ.app_one)
  rw [h‚ÇÄ‚ÇÅ.œÄ‚ÇÄFiberAction_eq, h‚ÇÅ‚ÇÇ.œÄ‚ÇÄFiberAction_eq, h.fac, (h‚ÇÄ‚ÇÅ.comp h‚ÇÅ‚ÇÇ h).œÄ‚ÇÄFiberAction_eq]

end

end KanComplex.FundamentalGroupoid

namespace Subcomplex

open KanComplex.FundamentalGroupoid

lemma neq_bot_iff {X : SSet.{u}} (A : X.Subcomplex) :
    A ‚â† ‚ä• ‚Üî (A.obj (op ‚¶ã0‚¶å)).Nonempty := by
  constructor
  ¬∑ rintro h
    by_contra! h'
    refine h (le_antisymm ?_ (by simp))
    rintro ‚ü®n‚ü© a ha
    simpa [h'] using A.map (SimplexCategory.const ‚¶ã0‚¶å n 0).op ha
  ¬∑ rintro h rfl
    simp at h

lemma fiber_neq_bot_iff_of_edge {E B : SSet.{u}} (p : E ‚ü∂ B) [Fibration p] {b‚ÇÄ b‚ÇÅ : B _‚¶ã0‚¶å}
    (edge : Edge (.mk b‚ÇÄ) (.mk b‚ÇÅ)) :
    (fiber p b‚ÇÄ ‚â† ‚ä•) ‚Üî (fiber p b‚ÇÅ ‚â† ‚ä•) := by
  simp only [neq_bot_iff]
  constructor
  ¬∑ rintro ‚ü®e‚ÇÄ, he‚ÇÄ‚ü©
    simp only [mem_fiber_obj_zero_iff] at he‚ÇÄ
    obtain ‚ü®e‚ÇÅ, ‚ü®h‚ü©‚ü© := FiberActionStruct.nonempty p edge e‚ÇÄ he‚ÇÄ
    exact ‚ü®e‚ÇÅ, by simp only [mem_fiber_obj_zero_iff, h.app_one]‚ü©
  ¬∑ rintro ‚ü®e‚ÇÅ, he‚ÇÅ‚ü©
    simp only [mem_fiber_obj_zero_iff] at he‚ÇÅ
    obtain ‚ü®e‚ÇÄ, ‚ü®h‚ü©‚ü© := FiberActionStruct.nonempty' p edge e‚ÇÅ he‚ÇÅ
    exact ‚ü®e‚ÇÄ, by simp only [mem_fiber_obj_zero_iff, h.app_zero]‚ü©

end Subcomplex

end SSet
