import TopCatModelCategory.SSet.Simplices
import Mathlib.AlgebraicTopology.SimplicialSet.Degenerate

universe u

open CategoryTheory Simplicial Limits Opposite

lemma Quiver.Hom.op_surjective {C : Type*} [Quiver C] {X Y : C·µí·µñ} (f : X ‚ü∂ Y) :
    ‚àÉ (g : Y.unop ‚ü∂ X.unop), f = g.op := ‚ü®f.unop, rfl‚ü©

instance {C : Type*} [Category C] {X Y : C} (f : X ‚ü∂ Y) [IsSplitEpi f] :
    IsSplitMono f.op where
  exists_splitMono := ‚ü®‚ü®(section_ f).op, Quiver.Hom.unop_inj (by simp)‚ü©‚ü©

lemma SimplexCategory.isIso_iff_len_eq_of_epi {n m : SimplexCategory} (f : n ‚ü∂ m) [Epi f] :
    IsIso f ‚Üî n.len = m.len := by
  have hf := len_le_of_epi f
  refine ‚ü®fun _ ‚Ü¶ le_antisymm (len_le_of_mono f) hf, fun h ‚Ü¶ ?_‚ü©
  obtain rfl : n = m := by aesop
  have h := (epi_iff_surjective (f := f)).1 inferInstance
  exact isIso_of_bijective ‚ü®by rwa [Finite.injective_iff_surjective], h‚ü©

namespace SSet

variable (X : SSet.{u})

structure N extends X.S where mk'' ::
  nonDegenerate : simplex ‚àà X.nonDegenerate _

namespace N

variable {X}

@[simps toS]
def mk' (s : X.S) (hs : s.2 ‚àà X.nonDegenerate _) : X.N where
  toS := s
  nonDegenerate := hs

lemma mk'_surjective (s : X.N) :
    ‚àÉ (t : X.S) (ht : t.2 ‚àà X.nonDegenerate _), s = mk' t ht :=
  ‚ü®s.toS, s.nonDegenerate, rfl‚ü©

@[simps]
def mk {n : ‚Ñï} (x : X _‚¶ãn‚¶å) (hx : x ‚àà X.nonDegenerate n) : X.N where
  simplex := x
  nonDegenerate := hx

lemma mk_surjective (x : X.N) :
    ‚àÉ (n : ‚Ñï) (y : X.nonDegenerate n), x = N.mk _ y.2 :=
  ‚ü®x.1.1, ‚ü®_, x.nonDegenerate‚ü©, rfl‚ü©

@[deprecated (since := "2025-08-06")] alias exists_nonDegenerate := mk_surjective

lemma induction
    {motive : ‚àÄ {n : ‚Ñï} (x : X _‚¶ãn‚¶å) (_ : x ‚àà X.nonDegenerate _), Prop}
    (h‚ÇÅ : ‚àÄ (x : X.N), motive x.1.2 x.2)
    {n : ‚Ñï} (x : X _‚¶ãn‚¶å) (hx : x ‚àà X.nonDegenerate _) : motive x hx :=
  h‚ÇÅ (mk x hx)

instance : Preorder X.N := Preorder.lift toS

lemma le_iff {x y : X.N} : x ‚â§ y ‚Üî Subcomplex.ofSimplex x.1.2 ‚â§ Subcomplex.ofSimplex y.1.2 :=
  Iff.rfl

lemma le_iff_exists {x y : X.N} :
    x ‚â§ y ‚Üî ‚àÉ (f : ‚¶ãx.1.1‚¶å ‚ü∂ ‚¶ãy.1.1‚¶å) (_ : Mono f), X.map f.op y.1.2 = x.1.2 := by
  simp only [le_iff, CategoryTheory.Subpresheaf.ofSection_le_iff,
    Subcomplex.mem_ofSimplex_obj_iff]
  refine ‚ü®?_, by tauto‚ü©
  rintro ‚ü®f, hf‚ü©
  refine ‚ü®f, ?_, hf‚ü©
  have : IsIso (factorThruImage f) := by
    rw [SimplexCategory.isIso_iff_len_eq_of_epi]
    obtain h | h := (SimplexCategory.len_le_of_epi
      (factorThruImage f)).eq_or_lt
    ¬∑ exact h.symm
    ¬∑ exfalso
      apply (mem_nonDegenerate_iff_notMem_degenerate _ _).1 x.2
      rw [mem_degenerate_iff]
      refine ‚ü®_, h, factorThruImage f, inferInstance, ?_‚ü©
      rw [‚Üê image.fac f, op_comp, FunctorToTypes.map_comp_apply] at hf
      rw [‚Üê hf]
      apply Set.mem_range_self
  have := isIso_of_mono_of_epi (factorThruImage f)
  rw [‚Üê image.fac f]
  infer_instance

lemma le_of_le {x y : X.N} (h : x ‚â§ y) : x.1.1 ‚â§ y.1.1 := by
  rw [le_iff_exists] at h
  obtain ‚ü®f, hf, _‚ü© := h
  exact SimplexCategory.len_le_of_mono f

instance : PartialOrder X.N where
  le_antisymm x‚ÇÅ x‚ÇÇ h h' := by
    obtain ‚ü®n‚ÇÅ, ‚ü®x‚ÇÅ, hx‚ÇÅ‚ü©, rfl‚ü© := x‚ÇÅ.mk_surjective
    obtain ‚ü®n‚ÇÇ, ‚ü®x‚ÇÇ, hx‚ÇÇ‚ü©, rfl‚ü© := x‚ÇÇ.mk_surjective
    obtain rfl : n‚ÇÅ = n‚ÇÇ := le_antisymm (le_of_le h) (le_of_le h')
    rw [le_iff_exists] at h
    obtain ‚ü®f, hf, h‚ü© := h
    obtain rfl := SimplexCategory.eq_id_of_mono f
    aesop

lemma eq_iff {x y : X.N} : x = y ‚Üî Subcomplex.ofSimplex x.1.2 = Subcomplex.ofSimplex y.1.2 :=
  ‚ü®by rintro rfl; rfl, fun h ‚Ü¶ by
    apply le_antisymm
    all_goals
    ¬∑ rw [le_iff, h]‚ü©

section

variable (s : X.N) {d : ‚Ñï} (hd : s.dim = d)

abbrev cast : X.N where
  toS := s.toS.cast hd
  nonDegenerate := by
    subst hd
    exact s.nonDegenerate

lemma cast_eq_self : s.cast hd = s := by
  subst hd
  rfl

end

end N

@[simps]
def orderEmbeddingN : X.N ‚Ü™o X.Subcomplex where
  toFun x := Subcomplex.ofSimplex x.1.2
  inj' _ _ h := by
    dsimp at h
    apply le_antisymm <;> rw [N.le_iff, h]
  map_rel_iff' := Iff.rfl

@[simps! obj]
def functorN : X.N ‚•§ SSet.{u} :=
  X.orderEmbeddingN.monotone.functor ‚ãô Subcomplex.toPresheafFunctor

variable {X} in
@[simp]
lemma functorN_map {x‚ÇÅ x‚ÇÇ : X.N} (f : x‚ÇÅ ‚ü∂ x‚ÇÇ) :
    X.functorN.map f = Subcomplex.homOfLE (X.orderEmbeddingN.monotone (leOfHom f)) := rfl

@[simps]
def coconeN : Cocone X.functorN where
  pt := X
  Œπ := { app _ := Subcomplex.Œπ _ }

section

variable {n : ‚Ñï} (x : X _‚¶ãn‚¶å)

noncomputable def toN : X.N :=
  N.mk _ (X.exists_nonDegenerate x).choose_spec.choose_spec.choose_spec.choose.2

noncomputable def toNœÄ : ‚¶ãn‚¶å ‚ü∂ ‚¶ã(X.toN x).1.1‚¶å :=
  (X.exists_nonDegenerate x).choose_spec.choose

instance : Epi (X.toNœÄ x) :=
  (X.exists_nonDegenerate x).choose_spec.choose_spec.choose

instance : IsSplitEpi (X.toNœÄ x) := isSplitEpi_of_epi _

@[simp]
lemma map_toNœÄ_op_toN : X.map (X.toNœÄ x).op (X.toN x).1.2 = x :=
  (X.exists_nonDegenerate x).choose_spec.choose_spec.choose_spec.choose_spec.symm

@[simp]
lemma map_splitEpiSection_eq_toN_snd (h : SplitEpi (X.toNœÄ x)) :
    X.map h.section_.op x = (X.toN x).1.2 := by
  nth_rw 6 [‚Üê X.map_toNœÄ_op_toN x]
  rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, h.id,
    op_id, FunctorToTypes.map_id_apply]

@[simp]
lemma map_section_eq_toN_snd :
    X.map (section_ (X.toNœÄ x)).op x = (X.toN x).1.2 :=
  map_splitEpiSection_eq_toN_snd _ _ (IsSplitEpi.exists_splitEpi (f := X.toNœÄ x)).some

@[simp]
lemma ofSimplex_toN : Subcomplex.ofSimplex (X.toN x).1.2 = Subcomplex.ofSimplex x := by
  refine le_antisymm ?_ ?_
  ¬∑ simp only [Subpresheaf.ofSection_le_iff, Subcomplex.mem_ofSimplex_obj_iff]
    have : IsSplitEpi (X.toNœÄ x) := isSplitEpi_of_epi _
    have h : Mono (X.map (X.toNœÄ x).op) := inferInstance
    rw [mono_iff_injective] at h
    exact ‚ü®section_ (X.toNœÄ x), by simp‚ü©
  ¬∑ simp only [Subpresheaf.ofSection_le_iff, Subcomplex.mem_ofSimplex_obj_iff]
    exact ‚ü®X.toNœÄ x, by simp‚ü©

variable {X} in
lemma N.ext' {n : ‚Ñï} (x : X _‚¶ãn‚¶å) (y‚ÇÅ y‚ÇÇ : X.N) (f‚ÇÅ : ‚¶ãn‚¶å ‚ü∂ ‚¶ãy‚ÇÅ.1.1‚¶å)
    (f‚ÇÇ : ‚¶ãn‚¶å ‚ü∂ ‚¶ãy‚ÇÇ.1.1‚¶å) [Epi f‚ÇÅ] [Epi f‚ÇÇ]
    (hf‚ÇÅ : X.map f‚ÇÅ.op y‚ÇÅ.1.2 = x) (hf‚ÇÇ : X.map f‚ÇÇ.op y‚ÇÇ.1.2 = x) : y‚ÇÅ = y‚ÇÇ := by
  obtain ‚ü®n‚ÇÅ, y‚ÇÅ, rfl‚ü© := y‚ÇÅ.mk_surjective
  obtain ‚ü®n‚ÇÇ, y‚ÇÇ, rfl‚ü© := y‚ÇÇ.mk_surjective
  obtain rfl := X.unique_nonDegenerate‚ÇÅ x _ _ hf‚ÇÅ.symm _ _ hf‚ÇÇ.symm
  obtain rfl := X.unique_nonDegenerate‚ÇÇ x _ _ hf‚ÇÅ.symm _ _ hf‚ÇÇ.symm
  rfl

lemma toN_eq {n : ‚Ñï} (x : X _‚¶ãn‚¶å) (y : X.N) (f : ‚¶ãn‚¶å ‚ü∂ ‚¶ãy.1.1‚¶å) [Epi f]
    (h : X.map f.op y.1.2 = x) : X.toN x = y :=
  N.ext' x _ _ (X.toNœÄ x) f (by simp) h

lemma toN_surjective (s : X.N) : ‚àÉ (n : ‚Ñï) (x : X.nonDegenerate n), s = X.toN x.1 :=
  ‚ü®s.1.1, ‚ü®_, s.2‚ü©, (X.toN_eq _ _ (ùüô _) (by simp)).symm‚ü©

end

namespace isColimitCoconeN

variable {X}

lemma hom_ext {Y : SSet.{u}} {f g : X ‚ü∂ Y}
    (h : ‚àÄ (x : X.N), (Subcomplex.ofSimplex x.1.2).Œπ ‚â´ f = (Subcomplex.ofSimplex x.1.2).Œπ ‚â´ g) :
    f = g := by
  rw [‚Üê cancel_epi (Subcomplex.topIso _).hom, ‚Üê Subpresheaf.equalizer_eq_iff,
    Subcomplex.eq_top_iff_contains_nonDegenerate]
  intro n x hx
  simpa [Subpresheaf.equalizer] using
    congr_fun (NatTrans.congr_app (h (N.mk _ hx)) (op ‚¶ãn‚¶å))
      ‚ü®x, Subcomplex.mem_ofSimplex_obj x‚ü©

variable (s : Cocone X.functorN)

noncomputable def descApp {n : ‚Ñï} (x : X _‚¶ãn‚¶å) : s.pt _‚¶ãn‚¶å :=
  yonedaEquiv (stdSimplex.map (X.toNœÄ x) ‚â´ Subcomplex.toOfSimplex _ ‚â´ s.Œπ.app (X.toN x))

lemma descApp_apply {n : ‚Ñï} (x : X _‚¶ãn‚¶å) (y : X.N) (f : ‚¶ãn‚¶å ‚ü∂ ‚¶ãy.1.1‚¶å) [Epi f]
    (hf : X.map f.op y.1.2 = x) :
    descApp s x = yonedaEquiv (stdSimplex.map f ‚â´ Subcomplex.toOfSimplex _ ‚â´ s.Œπ.app y) := by
  obtain rfl : y = X.toN x := by
    obtain ‚ü®m, y, rfl‚ü© := y.mk_surjective
    obtain rfl := X.unique_nonDegenerate‚ÇÅ x _ _ hf.symm _ _ ((X.map_toNœÄ_op_toN x).symm)
    obtain rfl := X.unique_nonDegenerate‚ÇÇ x _ _ hf.symm _ _ ((X.map_toNœÄ_op_toN x).symm)
    rfl
  obtain rfl := X.unique_nonDegenerate‚ÇÉ x _ _ hf.symm _ _ ((X.map_toNœÄ_op_toN x).symm)
  rfl

@[simp]
lemma descApp_apply' (x : X.N) :
    descApp s x.1.2 = (s.Œπ.app x).app _ ‚ü®x.1.2, Subcomplex.mem_ofSimplex_obj _‚ü© :=  by
  rw [descApp_apply s x.1.2 x (ùüô _) (by simp), CategoryTheory.Functor.map_id, Category.id_comp,
    yonedaEquiv_comp, Subcomplex.yonedaEquiv_toOfSimplex]

noncomputable def desc : X ‚ü∂ s.pt where
  app := fun ‚ü®n‚ü© ‚Ü¶ by
    induction' n using SimplexCategory.rec with n
    exact descApp _
  naturality := by
    rintro ‚ü®n‚ü© ‚ü®m‚ü© f
    obtain ‚ü®f, rfl‚ü© := Quiver.Hom.op_surjective f
    induction' n using SimplexCategory.rec with n
    induction' m using SimplexCategory.rec with m
    dsimp [SimplexCategory.rec]
    ext x
    dsimp [descApp]
    have h : X.toN (X.map f.op x) ‚â§ X.toN x := by
      rw [N.le_iff, ofSimplex_toN, ofSimplex_toN, Subpresheaf.ofSection_le_iff,
        Subcomplex.mem_ofSimplex_obj_iff]
      exact ‚ü®f, rfl‚ü©
    apply yonedaEquiv.symm.injective
    rw [Equiv.symm_apply_apply, SSet.yonedaEquiv_symm_map,
      Equiv.symm_apply_apply, ‚Üê s.w (homOfLE h)]
    dsimp
    simp only [‚Üê Category.assoc]; congr 1; simp only [Category.assoc]
    rw [‚Üê cancel_mono (Subcomplex.Œπ _)]
    apply yonedaEquiv.injective
    dsimp
    simp only [Category.assoc, Subcomplex.homOfLE_Œπ, Subcomplex.toOfSimplex_Œπ,
      yonedaEquiv_map_comp, Equiv.apply_symm_apply, map_toNœÄ_op_toN]

@[simp]
lemma desc_app_apply {n : ‚Ñï} (x : X _‚¶ãn‚¶å) :
    (desc s).app _ x = descApp _ x := rfl

@[reassoc (attr := simp)]
def fac (x : X.N) : (Subcomplex.ofSimplex x.1.2).Œπ ‚â´ desc s = s.Œπ.app x := by
  rw [‚Üê cancel_epi (Subcomplex.toOfSimplex _),
    Subcomplex.toOfSimplex_Œπ_assoc, yonedaEquiv_symm_comp, desc_app_apply,
    descApp_apply']
  apply yonedaEquiv.injective
  rw [Equiv.apply_symm_apply, yonedaEquiv_comp, Subcomplex.yonedaEquiv_toOfSimplex]

end isColimitCoconeN

noncomputable def isColimitCoconeN : IsColimit X.coconeN where
  desc := isColimitCoconeN.desc
  fac := isColimitCoconeN.fac
  uniq s m hm := isColimitCoconeN.hom_ext (by aesop)

variable {X} {Y : SSet.{u}} (f : X ‚ü∂ Y)

@[simps -isSimp coe]
noncomputable def mapN : X.N ‚Üío Y.N where
  toFun x := Y.toN (f.app _ x.1.2)
  monotone' x x' h := by
    simp only [N.le_iff, ofSimplex_toN, Subpresheaf.ofSection_le_iff,
      mem_ofSimplex_obj_iff] at h ‚ä¢
    obtain ‚ü®g, hf‚ü© := h
    exact ‚ü®g, by simp only [‚Üê hf, FunctorToTypes.naturality]‚ü©

@[simp]
lemma mapN_toN {n : ‚Ñï} (x : X _‚¶ãn‚¶å) :
    mapN f (X.toN x) = Y.toN (f.app _ x) := by
  have : IsSplitEpi (X.toNœÄ x) := isSplitEpi_of_epi _
  simp only [mapN_coe]
  apply le_antisymm
  ¬∑ simp only [N.le_iff, ofSimplex_toN, Subpresheaf.ofSection_le_iff,
      mem_ofSimplex_obj_iff]
    exact ‚ü®section_ (X.toNœÄ x), by rw [‚Üê FunctorToTypes.naturality, map_section_eq_toN_snd]‚ü©
  ¬∑ simp only [N.le_iff, ofSimplex_toN, Subpresheaf.ofSection_le_iff,
      mem_ofSimplex_obj_iff]
    exact ‚ü®X.toNœÄ x, by rw [‚Üê FunctorToTypes.naturality, map_toNœÄ_op_toN]‚ü©

@[simp]
lemma id_app {n : SimplexCategory·µí·µñ} (x : X.obj n) : NatTrans.app (ùüô X) n x = x := rfl

@[simp]
lemma mapN_id : mapN (ùüô X) = OrderHom.id := by
  ext x
  obtain ‚ü®n, x, rfl‚ü© := X.toN_surjective x
  simp

lemma mapN_mapN {Z : SSet.{u}} (g : Y ‚ü∂ Z) (x : X.N) :
    mapN g (mapN f x) = mapN (f ‚â´ g) x := by
  obtain ‚ü®n, x, rfl‚ü© := X.toN_surjective x
  simp

lemma N.ext_iff (x y : X.N) :
    x = y ‚Üî x.toS = y.toS := by
  obtain ‚ü®x, hx, rfl‚ü© := x.mk'_surjective
  obtain ‚ü®y, hy, rfl‚ü© := y.mk'_surjective
  simp [mk']

@[ext]
lemma N.ext {x y : X.N} (h : x.toS = y.toS) :
    x = y := by
  rwa [ext_iff]

lemma N.mk_eq_iff_sMk_eq {n m : ‚Ñï} (x : X _‚¶ãn‚¶å) (y : X _‚¶ãm‚¶å)
    (hx : x ‚àà X.nonDegenerate _) (hy : y ‚àà X.nonDegenerate _) :
    N.mk x hx = N.mk y hy ‚Üî S.mk x = S.mk y := by
  rw [ext_iff]
  rfl

namespace N

/-noncomputable def map : X.N ‚Üío Y.N :=
  ‚ü®fun s ‚Ü¶ Y.toN (s.1.map f).2, fun s t h ‚Ü¶ by
    dsimp
    simp only [le_iff, ofSimplex_toN, Subpresheaf.ofSection_le_iff] at h ‚ä¢
    obtain ‚ü®g, hg‚ü© := h
    refine ‚ü®g, ?_‚ü©
    dsimp
    rw [‚Üê FunctorToTypes.naturality, hg]‚ü©-/

attribute [local simp] mapN_mapN in
noncomputable def functor : SSet.{u} ‚•§ PartOrd.{u} where
  obj X := .of X.N
  map f := PartOrd.ofHom (mapN f)

end N

end SSet
