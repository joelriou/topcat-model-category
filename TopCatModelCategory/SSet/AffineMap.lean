import TopCatModelCategory.TopCat.Adj
import TopCatModelCategory.SSet.CoconeNPrime
import TopCatModelCategory.FunctorIterate
import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.Analysis.Convex.Combination
import Mathlib.Algebra.Module.BigOperators

open CategoryTheory Simplicial Opposite NNReal Limits

universe v u

namespace SSet

def vertexOfSimplex {X : SSet.{u}} {n : SimplexCategory}
    (x : X.obj (op n)) (i : Fin (n.len + 1)) : X _‚¶ã0‚¶å :=
  X.map (SimplexCategory.const ‚¶ã0‚¶å n i).op x

lemma vertexOfSimplex_map {X : SSet.{u}} {m n : SimplexCategory}
    (x : X.obj (op n)) (f : m ‚ü∂ n) (i : Fin (m.len + 1)) :
    vertexOfSimplex (X.map f.op x) i = vertexOfSimplex x (f i) := by
  dsimp [vertexOfSimplex]
  rw [‚Üê FunctorToTypes.map_comp_apply]
  rfl

end SSet

variable {n m : SimplexCategory}

namespace stdSimplex

def barycenter {X Œ± : Type*} [Fintype X] [Fintype Œ±] (p : Œ± ‚Üí stdSimplex ‚Ñù X)
    (w : Œ± ‚Üí ‚Ñù) (hw‚ÇÄ : ‚àÄ a, 0 ‚â§ w a) (hw‚ÇÅ : ‚àë a, w a = 1) : stdSimplex ‚Ñù X :=
  ‚ü®fun x ‚Ü¶ ‚àë (a : Œ±), w a ‚Ä¢ (p a).1 x,
    fun x ‚Ü¶ Finset.sum_nonneg (fun a _ ‚Ü¶ mul_nonneg (hw‚ÇÄ a) ((p a).2.1 x)), by
    dsimp
    rw [Finset.sum_comm, ‚Üê hw‚ÇÅ]
    congr
    ext a
    rw [‚Üê Finset.mul_sum, (p a).2.2, mul_one]‚ü©

@[simp]
lemma barycenter_apply
    {X Œ± : Type*} [Fintype X] [Fintype Œ±] (p : Œ± ‚Üí stdSimplex ‚Ñù X)
    (w : Œ± ‚Üí ‚Ñù) (hw‚ÇÄ : ‚àÄ a, 0 ‚â§ w a) (hw‚ÇÅ : ‚àë a, w a = 1) (x : X) :
    barycenter p w hw‚ÇÄ hw‚ÇÅ x = ‚àë (a : Œ±), w a ‚Ä¢ p a x := rfl

lemma eq_barycenter_vertex {X : Type*} [Fintype X] [DecidableEq X] (f : stdSimplex ‚Ñù X) :
    f = barycenter vertex f.1 f.2.1 f.2.2 := by
  ext x
  dsimp only [barycenter, DFunLike.coe]
  rw [Finset.sum_eq_single (a := x) _ (by simp), Pi.single_eq_same, smul_eq_mul, mul_one]
  intro y _ h
  rw [Pi.single_eq_of_ne' h, smul_zero]

lemma exists_barycenter_vertex {X : Type*} [Fintype X] [DecidableEq X] (f : stdSimplex ‚Ñù X) :
    ‚àÉ (w : X ‚Üí ‚Ñù) (hw‚ÇÄ : ‚àÄ x, 0 ‚â§ w x) (hw‚ÇÅ : ‚àë a, w a = 1),
      f = barycenter vertex w hw‚ÇÄ hw‚ÇÅ :=
  ‚ü®_, _, _, eq_barycenter_vertex f‚ü©

variable (n) in
noncomputable def isobarycenter (X : Type*) [Fintype X] [DecidableEq X] [Nonempty X] :
    stdSimplex ‚Ñù X :=
  barycenter vertex (fun _ ‚Ü¶ 1 / Fintype.card X) (by simp) (by simp)

@[simp]
lemma map_barycenter {X Y Œ± : Type*} [Fintype X] [Fintype Y] [Fintype Œ±]
    (g : X ‚Üí Y) (p : Œ± ‚Üí stdSimplex ‚Ñù X) (w : Œ± ‚Üí ‚Ñù) (hw‚ÇÄ : ‚àÄ a, 0 ‚â§ w a)
    (hw‚ÇÅ : ‚àë a, w a = 1) :
    map g (barycenter p w hw‚ÇÄ hw‚ÇÅ) = barycenter (fun a ‚Ü¶ map g (p a)) w hw‚ÇÄ hw‚ÇÅ := by
  classical
  ext y
  simp only [map_coe, barycenter_apply, smul_eq_mul, FunOnFinite.linearMap_apply_apply]
  rw [Finset.sum_comm]
  simp only [‚Üê Finset.mul_sum]

variable {X : Type*} [Fintype X]
variable {E : Type v} [AddCommGroup E] [Module ‚Ñù E] (f : stdSimplex ‚Ñù X ‚Üí E)

def IsAffine [DecidableEq X] : Prop :=
  ‚àÄ (p : stdSimplex ‚Ñù X), f p = ‚àë (x : X), p x ‚Ä¢ f (vertex x)

abbrev affineMap (p : X ‚Üí E) : stdSimplex ‚Ñù X ‚Üí E :=
  fun y ‚Ü¶ ‚àë (x : X), y x ‚Ä¢ p x

@[simp]
lemma affineMap_vertex [DecidableEq X] (p : X ‚Üí E) (x : X) :
    affineMap p (vertex x) = p x := by
  dsimp [affineMap, vertex]
  rw [Finset.sum_eq_single (a := x)]
  all_goals aesop

lemma isAffine_affineMap [DecidableEq X] (p : X ‚Üí E) :
    IsAffine (affineMap p) := by
  intro
  simp

variable (X) in
lemma affineMap_vertex_eq [DecidableEq X] :
    affineMap (Subtype.val ‚àò vertex : X ‚Üí _) = DFunLike.coe := by
  ext f x
  simp only [Finset.sum_apply, Function.comp_apply, Pi.smul_apply, smul_eq_mul]
  rw [Finset.sum_eq_single (a := x) _ (by simp), Pi.single_eq_same, mul_one]
  intro y _ h
  rw [Pi.single_eq_of_ne' h, mul_zero]

variable (X) in
lemma isAffine_dFunLikeCoe [DecidableEq X] :
    IsAffine (DFunLike.coe : stdSimplex ‚Ñù X ‚Üí _) := by
  rw [‚Üê affineMap_vertex_eq]
  apply isAffine_affineMap

lemma continuous_affineMap {E : Type v} [SeminormedAddCommGroup E] [NormedSpace ‚Ñù E]
    (p : X ‚Üí E) :
    Continuous (affineMap p) := by
  continuity

lemma IsAffine.postcomp {f : stdSimplex ‚Ñù X ‚Üí E} [DecidableEq X] (hf : IsAffine f)
    {E' : Type*} [AddCommGroup E'] [Module ‚Ñù E']
    (œÜ : E ‚Üí‚Çó[‚Ñù] E') : IsAffine (œÜ ‚àò f) :=
  fun x ‚Ü¶ by simp [hf x]

namespace IsAffine

variable {f} [DecidableEq X] (hf : IsAffine f)

include hf

lemma exists_eq :
    ‚àÉ (p : X ‚Üí E), f = affineMap p :=
  ‚ü®fun i ‚Ü¶ f (vertex i), by ext; rw [hf]‚ü©

lemma exists_linearMap :
    ‚àÉ (l : (X ‚Üí ‚Ñù) ‚Üí‚Çó[‚Ñù] E), f = l ‚àò Subtype.val := by
  obtain ‚ü®p, rfl‚ü© := hf.exists_eq
  exact ‚ü®Fintype.linearCombination ‚Ñù p, rfl‚ü©

lemma map_barycenter {Œ± : Type*} [Fintype Œ±] (p : Œ± ‚Üí stdSimplex ‚Ñù X) (w : Œ± ‚Üí ‚Ñù)
    (hw‚ÇÄ : ‚àÄ a, 0 ‚â§ w a) (hw‚ÇÅ : ‚àë a, w a = 1) :
    f (barycenter p w hw‚ÇÄ hw‚ÇÅ) = ‚àë (a : Œ±), w a ‚Ä¢ f (p a) := by
  obtain ‚ü®q, rfl‚ü© := hf.exists_eq
  simp only [Finset.smul_sum, affineMap]
  rw [Finset.sum_comm]
  congr
  ext x
  simp [smul_smul, Finset.sum_smul]

lemma map {Y : Type*} [Fintype Y] [DecidableEq Y] (g : Y ‚Üí X) :
    IsAffine (f.comp (stdSimplex.map g)) := by
  intro x
  obtain ‚ü®w, hw‚ÇÄ, hw‚ÇÅ, rfl‚ü© := exists_barycenter_vertex x
  dsimp
  rw [stdSimplex.map_barycenter, hf.map_barycenter]
  congr 1
  ext y
  simp only [map_vertex]
  congr 1
  rw [Finset.sum_eq_single (a := y) (by aesop) (by aesop),
    Pi.single_eq_same, mul_one]

lemma ext {g : stdSimplex ‚Ñù X ‚Üí E} (hg : IsAffine g)
    (h : ‚àÄ (x : X), f (vertex x) = g (vertex x)) : f = g := by
  ext x
  rw [hf, hg]
  simp [h]

lemma convex_range : Convex ‚Ñù (Set.range f) := by
  rintro _ ‚ü®x, rfl‚ü© _ ‚ü®y, rfl‚ü© a b ha hb h
  have := hf
  refine ‚ü®‚ü®fun i ‚Ü¶ a * x i + b * y i,
    fun i ‚Ü¶ add_nonneg (mul_nonneg ha (x.2.1 i))
      (mul_nonneg hb (y.2.1 i)), ?_‚ü©, ?_‚ü©
  ¬∑ simpa [Finset.sum_add_distrib, ‚Üê Finset.mul_sum]
  ¬∑ obtain ‚ü®p, rfl‚ü© := hf.exists_eq
    simp [affineMap, Finset.smul_sum, Finset.sum_add_distrib,
      DFunLike.coe, add_smul, smul_smul]

lemma map_barycenter_mem_of_convex
    {Œ± : Type*} [Fintype Œ±] (p : Œ± ‚Üí stdSimplex ‚Ñù X) (w : Œ± ‚Üí ‚Ñù)
    (hw‚ÇÄ : ‚àÄ a, 0 ‚â§ w a) (hw‚ÇÅ : ‚àë a, w a = 1)
    {S : Set E} (hS : Convex ‚Ñù S) (hq : ‚àÄ (a : Œ±), f (p a) ‚àà S) :
    f (barycenter p w hw‚ÇÄ hw‚ÇÅ) ‚àà S := by
  rw [hf.map_barycenter]
  exact hS.sum_mem (by simpa) hw‚ÇÅ (by simpa)

lemma range_subset_iff_of_convex {F : Set E} (hF : Convex ‚Ñù F) :
    Set.range f ‚äÜ F ‚Üî ‚àÄ i, f (vertex i) ‚àà F := by
  have := hf
  refine ‚ü®fun h i ‚Ü¶ h (by simp), fun h ‚Ü¶ ?_‚ü©
  rintro _ ‚ü®x, rfl‚ü©
  obtain ‚ü®w, hw‚ÇÄ, hw‚ÇÅ, rfl‚ü© := exists_barycenter_vertex x
  exact hf.map_barycenter_mem_of_convex _ _ _ _ hF h

lemma range_eq_convexHull :
    Set.range f = convexHull ‚Ñù (Set.range (fun i ‚Ü¶ f (vertex i))) := by
  apply subset_antisymm
  ¬∑ rw [hf.range_subset_iff_of_convex (convex_convexHull _ _)]
    intro i
    exact subset_convexHull _ _ (by simp)
  ¬∑ rw [(hf.convex_range).convexHull_subset_iff]
    grind

omit hf

lemma continuous {F : Type v} [SeminormedAddCommGroup F] [NormedSpace ‚Ñù F]
    (f : stdSimplex ‚Ñù X ‚Üí F) (hf : IsAffine f) :
    Continuous f := by
  obtain ‚ü®p, rfl‚ü© := hf.exists_eq
  exact continuous_affineMap p

end IsAffine

end stdSimplex

namespace SSet

variable {X Y : SSet.{u}} {E : Type v}

section

variable (f : |X| ‚Üí E)

namespace IsAffineAt

noncomputable def œÜ (x : X.obj (op n)) :
    _root_.stdSimplex ‚Ñù (Fin (n.len + 1)) ‚Üí E :=
  f.comp (Function.comp
    (toTopSimplex.inv.app _ ‚â´ toTop.map (yonedaEquiv.symm x)) ULift.up)

lemma precomp_œÜ (g : Y ‚ü∂ X) (y : Y.obj (op n)) :
    œÜ (f.comp (toTop.map g).hom) y = œÜ f (g.app _ y) := by
  dsimp [œÜ]
  rw [Function.comp_assoc, ‚Üê yonedaEquiv_symm_comp, Functor.map_comp]
  rfl

lemma map_œÜ {n : SimplexCategory} (x : X.obj (op n)) (g : m ‚ü∂ n) :
    œÜ f (X.map g.op x) = œÜ f x ‚àò stdSimplex.map g := by
  dsimp only [œÜ]
  rw [SSet.yonedaEquiv_symm_map]
  dsimp
  simp only [Functor.map_comp, TopCat.hom_comp, ContinuousMap.coe_comp, Function.comp_assoc]
  apply congr_arg
  apply congr_arg
  ext x
  exact ConcreteCategory.congr_hom
    (toTopSimplex.{u}.inv.naturality g).symm (ULift.up x)

end IsAffineAt

variable [AddCommGroup E] [Module ‚Ñù E]

def IsAffineAt {n : SimplexCategory} (x : X.obj (op n)) : Prop :=
  stdSimplex.IsAffine (IsAffineAt.œÜ f x)

variable {f} in
lemma IsAffineAt.map {n m : SimplexCategory} {x : X.obj (op n)}
    (hx : IsAffineAt f x) (g : m ‚ü∂ n) :
    IsAffineAt f (X.map g.op x) := by
  dsimp [IsAffineAt] at hx ‚ä¢
  rw [map_œÜ]
  exact hx.map g

lemma IsAffineAt.precomp {y : Y.obj (op n)} {g : Y ‚ü∂ X} (hy : IsAffineAt f (g.app _ y)) :
    IsAffineAt (f.comp (toTop.map g).hom) y := by
  dsimp [IsAffineAt] at hy ‚ä¢
  rw [precomp_œÜ]
  assumption

def IsAffine : Prop := ‚àÄ ‚¶Én : SimplexCategory‚¶Ñ (x : X.obj (op n)), IsAffineAt f x

def isAffine : X.Subcomplex where
  obj := fun ‚ü®_‚ü© ‚Ü¶ setOf (fun x ‚Ü¶ IsAffineAt f x)
  map _ _ hx := hx.map _

@[simp]
lemma mem_isAffine_iff {n : SimplexCategory} (x : X.obj (op n)) :
    x ‚àà (isAffine f).obj _ ‚Üî IsAffineAt f x := Iff.rfl

lemma isAffine_iff_eq_top : IsAffine f ‚Üî isAffine f = ‚ä§ := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h n x ‚Ü¶ ?_‚ü©
  ¬∑ ext ‚ü®n‚ü© x
    simpa using h x
  ¬∑ simp [‚Üê mem_isAffine_iff, h]

section

variable {f} {E' : Type*} [AddCommGroup E'] [Module ‚Ñù E']

lemma IsAffineAt.postcomp
    {x : X.obj (op n)} (hx : IsAffineAt f x) (œÜ : E ‚Üí‚Çó[‚Ñù] E') :
    IsAffineAt (œÜ ‚àò f) x :=
  stdSimplex.IsAffine.postcomp hx œÜ

lemma IsAffine.postcomp
    (hf : IsAffine f) (œÜ : E ‚Üí‚Çó[‚Ñù] E') :
    IsAffine (œÜ ‚àò f) :=
  fun _ x ‚Ü¶ (hf x).postcomp œÜ

end

end

variable [AddCommGroup E] [Module ‚Ñù E]

variable (X E)
structure AffineMap where
  f : |X| ‚Üí E
  isAffine : IsAffine f

namespace AffineMap

variable {X E} (f : AffineMap X E)

noncomputable abbrev œÜ {d : SimplexCategory} (s : X.obj (op d)) :
    _root_.stdSimplex ‚Ñù (Fin (d.len + 1)) ‚Üí E :=
  IsAffineAt.œÜ f.f s

lemma continuous_œÜ {E : Type v} [SeminormedAddCommGroup E] [NormedSpace ‚Ñù E]
    (f : AffineMap X E)
    {n : SimplexCategory} (x : X.obj (op n)) : Continuous (f.œÜ x) :=
  (f.isAffine x).continuous

lemma map_œÜ {d e : SimplexCategory} (s : X.obj (op d)) (g : e ‚ü∂ d) :
    f.œÜ (X.map g.op s) = f.œÜ s ‚àò stdSimplex.map g := by
  simp [IsAffineAt.map_œÜ]

lemma range_subset_of_le {s t : X.S} (hst : s ‚â§ t) :
    Set.range (f.œÜ s.simplex) ‚äÜ Set.range (f.œÜ t.simplex) := by
  obtain ‚ü®d, s, rfl‚ü© := s.mk_surjective
  obtain ‚ü®e, t, rfl‚ü© := t.mk_surjective
  rw [S.le_iff, Subpresheaf.ofSection_le_iff,
    Subcomplex.mem_ofSimplex_obj_iff] at hst
  dsimp at hst ‚ä¢
  obtain ‚ü®g, rfl‚ü© := hst
  rw [map_œÜ]
  grind

noncomputable def isobarycenter (s : X.S) : E :=
  f.œÜ s.simplex (stdSimplex.isobarycenter _)

lemma range_œÜ_subset_range_f (s : X.S) :
    Set.range (f.œÜ s.simplex) ‚äÜ Set.range f.f := by
  dsimp [œÜ, IsAffineAt.œÜ]
  grind

lemma range_f_eq' :
    Set.range f.f = ‚ãÉ (s : X.S), Set.range (f.œÜ s.simplex) := by
  apply subset_antisymm
  ¬∑ rintro _ ‚ü®x, rfl‚ü©
    obtain ‚ü®‚ü®‚ü®n‚ü©, s‚ü©, x, rfl‚ü© := Types.jointly_surjective_of_isColimit
      (isColimitOfPreserves (toTop ‚ãô forget _)
      (X.isColimitCoconeFromElementsOp)) x
    induction n using SimplexCategory.rec with | _ n
    dsimp at x ‚ä¢
    obtain ‚ü®x, rfl‚ü© := (TopCat.homeoOfIso (toTopSimplex.{u}.app ‚¶ãn‚¶å)).toEquiv.symm.surjective x
    simp only [Set.mem_iUnion]
    exact ‚ü®S.mk s, ULift.down x, rfl‚ü©
  ¬∑ simpa only [Set.iUnion_subset_iff] using f.range_œÜ_subset_range_f

lemma range_f_eq :
    Set.range f.f = ‚ãÉ (s : X.N), Set.range (f.œÜ s.simplex) := by
  rw [range_f_eq']
  apply subset_antisymm
  ¬∑ simp only [Set.iUnion_subset_iff]
    intro s
    refine le_trans ?_ (le_iSup _ s.toN)
    exact f.range_subset_of_le s.self_le_toS_toN
  ¬∑ simp only [Set.iUnion_subset_iff]
    intro s
    exact le_trans (by rfl) (le_iSup _ s.toS)

noncomputable def precomp (g : Y ‚ü∂ X) : AffineMap Y E where
  f := Function.comp f.f (toTop.map g).hom
  isAffine _ y := (f.isAffine (g.app _ y)).precomp

lemma range_precomp_f_subset_range_f (g : Y ‚ü∂ X) :
    Set.range (f.precomp g).f ‚äÜ Set.range f.f := by
  dsimp [precomp]
  apply Set.range_comp_subset_range

@[simp]
lemma precomp_œÜ (g : Y ‚ü∂ X) {d : SimplexCategory} (y : Y.obj (op d)) :
    (f.precomp g).œÜ y = f.œÜ (g.app _ y) :=
  IsAffineAt.precomp_œÜ f.f g y

@[simps -fullyApplied f]
def postcomp {E' : Type*} [AddCommGroup E'] [Module ‚Ñù E'] (œÜ : E ‚Üí‚Çó[‚Ñù] E') :
    AffineMap X E' where
  f := œÜ ‚àò f.f
  isAffine := f.isAffine.postcomp œÜ

@[continuity]
lemma continuous {E : Type v} [SeminormedAddCommGroup E] [NormedSpace ‚Ñù E]
    (f : X.AffineMap E) :
    Continuous f.f := by
  rw [TopCat.continuous_iff_of_isColimit' _
    (isColimitOfPreserves toTop (Presheaf.colimitOfRepresentable X))]
  rintro ‚ü®‚ü®n‚ü©, x‚ü©
  convert ((f.continuous_œÜ x).comp
    Homeomorph.ulift.continuous).comp (toTopSimplex.hom.app n).hom.continuous
  ext x
  dsimp [œÜ, IsAffineAt.œÜ] at x ‚ä¢
  congr
  exact (ConcreteCategory.congr_hom (toTopSimplex.app n).hom_inv_id x).symm

@[simp]
lemma isobarycenter_precomp (g : Y ‚ü∂ X) (s : Y.S) :
    (f.precomp g).isobarycenter s = f.isobarycenter (s.map g) := by
  simp [isobarycenter]

noncomputable def vertex (x : X _‚¶ã0‚¶å) : E := f.œÜ x default

lemma œÜ_vertex {n : SimplexCategory} (x : X.obj (op n)) (i : Fin (n.len + 1)) :
    f.œÜ x (stdSimplex.vertex i) = f.vertex (vertexOfSimplex x i) := by
  have h‚ÇÅ := congr_fun (f.map_œÜ x (SimplexCategory.const ‚¶ã0‚¶å n i)) default
  have h‚ÇÇ := stdSimplex.map_vertex (S := ‚Ñù) (SimplexCategory.const ‚¶ã0‚¶å n i) 0
  dsimp [vertex, vertexOfSimplex] at h‚ÇÅ ‚ä¢
  rw [SimplexCategory.const_apply'] at h‚ÇÇ
  rw [h‚ÇÅ, ‚Üê h‚ÇÇ]
  congr
  subsingleton

@[simp]
lemma precomp_vertex (f : X.AffineMap E) (g : Y ‚ü∂ X) (y : Y _‚¶ã0‚¶å) :
    (f.precomp g).vertex y = f.vertex (g.app _ y) := by
  simp [vertex]

@[simp]
lemma postcomp_vertex (f : X.AffineMap E)
    {E' : Type*} [AddCommGroup E'] [Module ‚Ñù E'] (œÜ : E ‚Üí‚Çó[‚Ñù] E') (x : X _‚¶ã0‚¶å) :
    (f.postcomp œÜ).vertex x = œÜ (f.vertex x) :=
  rfl

@[ext]
lemma ext {f g : X.AffineMap E}
    (h : ‚àÄ (x : X _‚¶ã0‚¶å), f.vertex x = g.vertex x) : f = g := by
  suffices f.f = g.f by cases f; cases g; aesop
  suffices ‚àÄ {d : SimplexCategory} (s : X.obj (op d)), f.œÜ s = g.œÜ s by
    ext y
    obtain ‚ü®‚ü®‚ü®d‚ü©, s‚ü©, z, rfl‚ü©:= Types.jointly_surjective_of_isColimit
      (isColimitOfPreserves (toTop ‚ãô forget _) (isColimitCoconeFromElementsOp X)) y
    dsimp at z ‚ä¢
    replace this := congr_fun (this s) (d.toTopHomeo z)
    dsimp [œÜ, IsAffineAt.œÜ] at this
    have hz : z = toTopSimplex.{u}.inv.app d (ULift.up (d.toTopHomeo z)) := by
      obtain ‚ü®z, rfl‚ü© := d.toTopHomeo.symm.surjective z
      simp
      rfl
    convert this
  intro d s
  exact stdSimplex.IsAffine.ext (f.isAffine s) (g.isAffine s)
    (fun i ‚Ü¶ by simp [œÜ_vertex, h])

namespace b

noncomputable def affineMap (s : (B.obj X).N) :
    _root_.stdSimplex ‚Ñù (Fin (s.dim + 1)) ‚Üí E :=
  (stdSimplex.affineMap
    (fun i ‚Ü¶ f.isobarycenter (s.simplex.obj i).toS))

lemma isAffine_affineMap (s : (B.obj X).N) :
    stdSimplex.IsAffine (affineMap f s) :=
  stdSimplex.isAffine_affineMap _
lemma affineMap_comp {s t : (B.obj X).N} (hst : s ‚â§ t) :
    (affineMap f t).comp (stdSimplex.map (N.monoOfLE hst)) =
      affineMap f s := by
  refine stdSimplex.IsAffine.ext
    (stdSimplex.IsAffine.map
      (stdSimplex.isAffine_affineMap _) _)
    (stdSimplex.isAffine_affineMap _) (fun i ‚Ü¶ ?_)
  dsimp [affineMap]
  simp only [stdSimplex.map_vertex, stdSimplex.affineMap_vertex,
    ‚Üê N.map_monoOfLE hst]
  rfl

lemma range_affineMap_le (s : (B.obj X).N) (t : X.N) (hs : s.simplex.obj (Fin.last _) ‚â§ t) :
    Set.range (affineMap f s) ‚äÜ Set.range (f.œÜ t.simplex) := by
  rw [(isAffine_affineMap f s).range_subset_iff_of_convex
    (f.isAffine t.simplex).convex_range]
  intro i
  dsimp only [affineMap]
  rw [stdSimplex.affineMap_vertex]
  exact (f.isAffine (s.simplex.obj i).simplex).map_barycenter_mem_of_convex _ _ _ _
    (f.isAffine t.simplex).convex_range
    (fun a ‚Ü¶ f.range_subset_of_le ((s.simplex.monotone i.le_last).trans hs) (by simp))

noncomputable def coconeTypes :
    ((B.obj X).functorN' ‚ãô toTop ‚ãô forget TopCat).CoconeTypes where
  pt := E
  Œπ s := Function.comp (affineMap f s) ‚¶ãs.dim‚¶å.toTopHomeo
  Œπ_naturality {s t} hst := by
    rw [‚Üê affineMap_comp f (leOfHom hst), Function.comp_assoc, Function.comp_assoc]
    apply congr_arg
    exact SimplexCategory.toTopHomeo_naturality (N.monoOfLE (leOfHom hst))

noncomputable def g : |B.obj X| ‚Üí E :=
  ((Types.isColimit_iff_coconeTypesIsColimit _).1
    ‚ü®isColimitOfPreserves (toTop ‚ãô forget _) (B.obj X).isColimitCoconeN'‚ü©).desc
      (coconeTypes f)

lemma comp_g_toTop_map (s : (B.obj X).N) :
    Function.comp (g f) (toTop.map (yonedaEquiv.symm s.simplex)) =
      Function.comp (affineMap f s) ‚¶ãs.dim‚¶å.toTopHomeo := by
  dsimp
  exact
  ((Types.isColimit_iff_coconeTypesIsColimit _).1
    ‚ü®isColimitOfPreserves (toTop ‚ãô forget _) (B.obj X).isColimitCoconeN'‚ü©).fac
      (coconeTypes f) s

lemma isAffineAtœÜ_g (s : (B.obj X).N) :
    IsAffineAt.œÜ (g f) s.simplex =
      stdSimplex.affineMap
        (fun i ‚Ü¶ f.isobarycenter (s.simplex.obj i).toS) := by
  dsimp [IsAffineAt.œÜ]
  rw [‚Üê Function.comp_assoc, ‚Üê Function.comp_assoc]
  erw [comp_g_toTop_map]
  dsimp only [affineMap]
  rw [Function.comp_assoc, Function.comp_assoc]
  convert Function.comp_id _
  dsimp [SimplexCategory.toTopHomeo]
  ext x : 1
  exact congr_arg ULift.down (congr_fun ((forget _).congr_map
    (toTopSimplex.{u}.inv_hom_id_app ‚¶ãs.dim‚¶å)) (ULift.up x))

lemma isAffine_g : IsAffine (g f) := by
  rw [isAffine_iff_eq_top, Subcomplex.eq_top_iff_contains_nonDegenerate]
  intro n x hx
  dsimp [SSet.isAffine, IsAffineAt]
  have := isAffineAtœÜ_g f (N.mk x hx)
  dsimp at this
  rw [this]
  exact stdSimplex.isAffine_affineMap _

end b

noncomputable def b : AffineMap (B.obj X) E := ‚ü®_, b.isAffine_g f‚ü©

lemma b_œÜ (s : (B.obj X).N) :
    f.b.œÜ s.simplex = b.affineMap f s :=
  b.isAffineAtœÜ_g _ _

lemma range_b_f_subset_range_f : Set.range f.b.f ‚äÜ Set.range f.f := by
  simp only [range_f_eq, Set.iUnion_subset_iff]
  intro s
  rw [b_œÜ]
  exact (b.range_affineMap_le f s _ (le_refl _)).trans
    (le_trans (by rfl) (le_iSup _ (s.simplex.obj (Fin.last _))))

lemma œÜ_barycenter (s : X.obj (op n))
    {Œ± : Type*} [Fintype Œ±] (p : Œ± ‚Üí _root_.stdSimplex ‚Ñù (Fin (n.len + 1))) (w : Œ± ‚Üí ‚Ñù)
    (hw‚ÇÄ : ‚àÄ a, 0 ‚â§ w a) (hw‚ÇÅ : ‚àë a, w a = 1) :
    f.œÜ s (stdSimplex.barycenter p w hw‚ÇÄ hw‚ÇÅ) =
      Finset.centerMass (R := ‚Ñù) (Finset.univ) (fun a ‚Ü¶ w a) (fun a ‚Ü¶ f.œÜ s (p a)) := by
  dsimp [œÜ]
  rw [(f.isAffine s).map_barycenter, Finset.centerMass_eq_of_sum_1 _ _ hw‚ÇÅ]

lemma range_œÜ_eq_convexHull {d : SimplexCategory} (x : X.obj (op d)) :
    Set.range (f.œÜ x) =
      convexHull ‚Ñù (Set.range (fun (i : Fin (d.len + 1)) ‚Ü¶ f.vertex (vertexOfSimplex x i))) := by
  simp only [(f.isAffine x).range_eq_convexHull, ‚Üê f.œÜ_vertex]

@[simp]
lemma vertex_b (x : (B.obj X) _‚¶ã0‚¶å) :
    f.b.vertex x = f.isobarycenter (x.obj 0).toS := by
  have := f.b_œÜ (N.mk x (by simp))
  dsimp [b.affineMap] at this
  dsimp only [vertex]
  rw [this]
  convert stdSimplex.affineMap_vertex (fun i ‚Ü¶ f.isobarycenter (x.obj i).toS) 0
  subsingleton

lemma isobarycenter_eq_centerMass (s : X.S) :
    f.isobarycenter s = Finset.univ.centerMass (fun _ ‚Ü¶ (1 : ‚Ñù))
        (fun i ‚Ü¶ f.vertex (vertexOfSimplex s.simplex i)) := by
  dsimp [isobarycenter, stdSimplex.isobarycenter]
  simp [œÜ_barycenter, œÜ_vertex]
  rw [‚Üê Finset.centerMass_smul_left (c := (s.dim : ‚Ñù) + 1)]
  ¬∑ congr
    ext
    simpa using mul_inv_cancel‚ÇÄ (by positivity)
  ¬∑ positivity

lemma b_precomp (g : Y ‚ü∂ X) [Mono g] :
    (f.precomp g).b = f.b.precomp (B.map g) := by
  ext s
  dsimp
  rw [vertex_b, isobarycenter_precomp, precomp_vertex, vertex_b, B_map_app_obj,
    toS_mapN_of_nonDegenerate]
  rw [nonDegenerate_iff_of_mono]
  exact (s.obj 0).nonDegenerate

protected noncomputable def sd : (sd.obj X).AffineMap E := f.b.precomp (sdToB.app X)

lemma range_sd_f_subset_range_f : Set.range f.sd.f ‚äÜ Set.range f.f :=
  (f.b.range_precomp_f_subset_range_f _).trans f.range_b_f_subset_range_f

noncomputable def sdIter (n : ‚Ñï) : ((sd.iter n).obj X).AffineMap E := by
  induction n with
  | zero => exact f
  | succ n hn => exact hn.sd

@[simp]
lemma sdIter_zero : f.sdIter 0 = f := rfl

@[simp]
lemma sdIter_succ (n : ‚Ñï) : f.sdIter (n + 1) = (f.sdIter n).sd := rfl

lemma range_sdIter_f_subset_range_f (n : ‚Ñï) : Set.range (f.sdIter n).f ‚äÜ Set.range f.f := by
  induction n with
  | zero => rfl
  | succ n hn => exact (range_sd_f_subset_range_f _).trans hn

lemma isAffineAtœÜ_toTopHomeo (n : ‚Ñï) :
    (IsAffineAt.œÜ (fun s i ‚Ü¶ ‚¶ãn‚¶å.toTopHomeo s i)
      (SSet.yonedaEquiv.{u} (ùüô _))) = DFunLike.coe := by
  ext f i
  simpa [IsAffineAt.œÜ] using DFunLike.congr_fun (‚¶ãn‚¶å.toTopHomeo.right_inv f) i

noncomputable def stdSimplex (n : ‚Ñï) :
    (Œî[n] : SSet.{u}).AffineMap (Fin (n + 1) ‚Üí ‚Ñù) where
  f := DFunLike.coe ‚àò ‚¶ãn‚¶å.toTopHomeo
  isAffine := by
    rw [isAffine_iff_eq_top, stdSimplex.subcomplex_eq_top_iff, mem_isAffine_iff]
    dsimp only [IsAffineAt]
    erw [isAffineAtœÜ_toTopHomeo]
    exact stdSimplex.isAffine_dFunLikeCoe (Fin (n + 1))

@[simp]
lemma stdSimplex_f_toTopHomeo_symm {n : ‚Ñï}  (x : _root_.stdSimplex ‚Ñù (Fin (n + 1))) :
    (stdSimplex n).f (‚¶ãn‚¶å.toTopHomeo.symm x) = x := by
  simpa [stdSimplex] using ‚¶ãn‚¶å.toTopHomeo.right_inv x

lemma injective_stdSimplex_f (n : ‚Ñï) :
    Function.Injective (stdSimplex n).f := by
  intro x y h
  obtain ‚ü®x, rfl‚ü© := ‚¶ãn‚¶å.toTopHomeo.symm.surjective x
  obtain ‚ü®y, rfl‚ü© := ‚¶ãn‚¶å.toTopHomeo.symm.surjective y
  simp only [stdSimplex_f_toTopHomeo_symm] at h
  obtain rfl : x = y := by aesop
  rfl

end AffineMap

end SSet
